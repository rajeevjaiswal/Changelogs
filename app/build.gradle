apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'realm-android'
apply plugin: 'com.jakewharton.hugo'

def keystorePropertiesFile = rootProject.file("/app/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.saladevs.changelogclone"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
    }
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField 'String', 'API_KEY', ReleaseApiKey
            buildConfigField 'String', 'API_BASE_URL', ProductionApiUrl
            signingConfig signingConfigs.release
            resValue "string", "app_name", "Changes"
        }
        debug {
            buildConfigField 'String', 'API_KEY', DebugApiKey
            buildConfigField 'String', 'API_BASE_URL', DevelopmentApiUrl
            resValue "string", "app_name", "Changes Debug"
            applicationIdSuffix ".debug"
        }
    }
}

dependencies {
    final SUPPORT_LIBRARY_VERSION = '25.1.1'
    final OKHTTP_VERSION = '3.5.0'
    final RETROFIT_VERSION = '2.1.0'

    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:cardview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:support-annotations:$SUPPORT_LIBRARY_VERSION"

    compile "com.squareup.okhttp3:okhttp:$OKHTTP_VERSION"
    compile "com.squareup.okhttp3:logging-interceptor:$OKHTTP_VERSION"
    compile "com.squareup.retrofit2:retrofit:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:converter-gson:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:adapter-rxjava:$RETROFIT_VERSION"

    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.jakewharton.timber:timber:4.3.1'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'org.apache.commons:commons-lang3:3.5'
    compile 'com.jonathanfinerty.once:once:1.2.1'
    compile 'org.javatuples:javatuples:1.2'

    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.2.4'
    compile 'com.artemzin.rxjava:proguard-rules:1.2.4.0'
    compile 'com.f2prateek.rx.preferences:rx-preferences:1.0.2'

}
